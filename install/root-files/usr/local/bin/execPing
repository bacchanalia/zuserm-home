#!/usr/bin/perl
use strict;
use warnings;

sub attemptPing($$$);

my $defaultDeadline = 1;
my $defaultDelay = 1;
my $defaultTimeout = 0;

my $usage = "Usage:
  $0 [OPTS] URL EXEC [ARG ARG ..]
    Ensure that URL is ping-able before running \"EXEC ARG ARG ..\"

  OPTS:
    --deadline=INTEGER     {default is $defaultDeadline}
      seconds to wait for a response for each ping

    --delay=INTEGER        {default is $defaultDelay}
      seconds to wait between failed pings

    --timeout=INTEGER  {default is $defaultTimeout}
      seconds to wait before failing, 0 means forever

    --ipmagic
      interpret URL as an ipmagic conf, and use `ipmagic URL --ip` as the url
      also, if the IP is not ping-able, tries SSH as a fallback
";

sub main(@){
  my ($deadline, $delay, $timeout) =
    ($defaultDeadline, $defaultDelay, $defaultTimeout);
  my $useIpmagic = 0;
  while(@_ > 0 and $_[0] =~ /^-/){
    my $opt = shift;
    if($opt =~ /^--deadline=(\d+)$/){
      $deadline = $1;
    }elsif($opt =~ /^--delay=(\d+)$/){
      $delay = $1;
    }elsif($opt =~ /^--timeout=(\d+)$/){
      $timeout = $1;
    }elsif($opt =~ /^--ipmagic$/){
      $useIpmagic = 1;
    }else{
      die $usage;
    }
  }

  my $url = shift;
  my $exec = shift;
  my @args = @_;

  die $usage if not defined $url or not defined $exec;

  my $start = time;
  while(not attemptPing $url, $useIpmagic, $deadline){
    if($timeout > 0 and time - $start > $timeout){
      die "timeout reached, exiting\n";
    }
    print STDERR "ping failed, retrying...\n";
    sleep $delay;
  }

  exec $exec, @args;
}

sub attemptPing($$$){
  my ($url, $useIpmagic, $deadline) = @_;
  my $ipmagicConf;
  if($useIpmagic){
    $ipmagicConf = $url;
    $url = `ipmagic "$ipmagicConf" --ip`;
    chomp $url;
    print "Using \"$url\"\n";
  }
  $url =~ s/'/'\\''/g;
  system "ping -c 1 -w $deadline '$url' >/dev/null 2>/dev/null";
  my $success = $? == 0;
  if(not $success and $useIpmagic){
    system "ipmagic", $ipmagicConf, "echo ssh $ipmagicConf success!\n";
    $success = $? == 0;
  }
  return $success;
}

&main(@ARGV);
