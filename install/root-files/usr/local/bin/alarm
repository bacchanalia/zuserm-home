#!/usr/bin/perl
use strict;
use warnings;
use Getopt::Long qw(GetOptionsFromArray :config bundling);
use Cwd 'abs_path';

sub listFiles($);
sub pulseExec(@);

my $alarmsDir = '/usr/share/alarms';
die "$alarmsDir/ doesnt exist or isnt a dir\n" if not -d $alarmsDir;
my $validAlarms = join '|', listFiles $alarmsDir;
die "$alarmsDir is empty\n" if $validAlarms eq '';

my $usage = "Usage:
  $0 [OPTS] [$validAlarms]   {default is 'default'}
    Runs 'term mplayer $alarmsDir/NAME'.
    If run as root, reruns as the user running pulseaudio.
    OPTS:
      -h|--help       Show this message
      -n|--norerun    Do not rerun as pulse user
      -o|--once       Do not repeat alarm {don't pass '-loop 0' to mplayer}
      -g|--guess      If alarm does not match, use 'default' instead of failing
      -v N|--vol=N    Attempt to unmute and set volume to N % using pulse-vol
";


sub main(@){
  my ($help, $norerun, $once, $guess, $vol);
  my $ok = GetOptionsFromArray(\@_,
    "h|help"       => \$help,
    "n|norerun"    => \$norerun,
    "o|once"       => \$once,
    "g|guess"      => \$guess,
    "v|vol=i"      => \$vol,
  );

  my $name = shift() || 'default';
  if(defined $guess and $name !~ /^($validAlarms)$/){
    print "using 'default' instead of '$name'\n";
    $name = 'default';
  }

  die $usage if not $ok or @_ > 0
    or defined $help
    or $name !~ /^($validAlarms)$/
    or (defined $vol and $vol < 0)
    ;

  pulseExec $0, @ARGV unless defined $norerun;

  my $alarmFile = "$alarmsDir/$name";
  $alarmFile = abs_path $alarmFile if -l $alarmFile;
  die "Missing alarm file $alarmFile\n" if not -e $alarmFile;

  if(defined $vol){
    system "pulse-vol", "unmute";
    system "pulse-vol", $vol;
  }

  my @cmd = ("term", "mplayer", "$alarmsDir/$name");
  @cmd = (@cmd, "-loop", "0") unless $once;
  exec @cmd;
}

sub listFiles($){
  return sort map {chomp; $_} `ls $_[0]/`;
}

sub pulseExec(@){
  my $user = `whoami`;
  chomp $user;
  my $pulseUser = `ps --no-heading -o user -C pulseaudio | head -1`;
  chomp $pulseUser;

  if($user eq 'root' and $pulseUser ne 'root' and length($pulseUser) > 0){
    print "rerunning as pulse user $pulseUser\n";
    exec "su", $pulseUser, "-c", "@_";
  }
}

&main(@ARGV);
